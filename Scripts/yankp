#!/usr/bin/env zsh

# Description: Lookup full path to files/directories and copy list to
# clipboard. Flag --nnn starts non-interactive mode for use as nnn plugin.
#
# ~/.zshrc:
#
#   export NNN_PLUG='y:-!yankp --nnn*'
#
# Usage: yankp <file> ...
#        yankp .
#        yankp --nnn
#
# Author: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ $# -lt 1 ]]; then
  echo "Usage: ${0:t} [OPTION]... [PATH(S)]...

  -n, --nnn  Run as non-interactive nnn plugin."
  exit
fi

if whence -p pbcopy &> /dev/null; then
  copy_cmd=pbcopy
elif whence -p xsel &> /dev/null; then
  copy_cmd=xsel
else
  echo "Failed to find clipboard command! Exiting..."
  exit 1
fi

if [[ $1 == (-n|--nnn) ]]; then
  selection=${NNN_SEL:-${XDG_CONFIG_HOME:-$HOME/.config}/nnn/.selection}
  if [[ -s $selection ]]; then
    content=$(tr '\0' '\n' < $selection)
    echo -n $content | eval $copy_cmd
  elif [[ -s $nnn ]]; then
    echo -n ${nnn:a} | eval $copy_cmd
  fi
else
  for arg; do
    if [[ -e $arg ]]; then
      list+=(${arg:a})
    fi
  done
  if [[ -n $list ]]; then
    content=$(printf "%s\n" $list)
    echo $content
    echo -n $content | eval $copy_cmd
  fi
fi
