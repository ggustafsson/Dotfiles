#!/usr/bin/env zsh

# Description: Run git pull on all Git repos under default projects path, or
# path specified through argument.
#
# Usage: gitpl
#        gitpl <path>
#
# Author: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ $# -eq 0 ]]; then
  projects_path=~/Projects
elif [[ $# -eq 1 ]]; then
  projects_path=$1
else
  echo "Usage: ${0:t} [PATH]..."
  exit
fi

if [[ ! -d $projects_path ]]; then
  echo "Directory '${projects_path}' not found! Exiting..."
  exit 1
fi

echo "Updating all Git repos under '${projects_path/${HOME}/~}'..."

for directory in ${projects_path}/*(N); do
  if ! git -C $directory rev-parse --is-inside-work-tree &> /dev/null; then
    continue
  else
    found_repo=1
  fi

  print -P "\n%B%F{cyan}${directory:t}:%f%b"
  git -C $directory pull
done

if [[ $found_repo -ne 1 ]]; then
  echo "No Git repos found."
fi
