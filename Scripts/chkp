#!/usr/bin/env zsh

# Written by GÃ¶ran Gustafsson <gustafsson.g@gmail.com>.

# Description: This script checks the git status of all my projects.

# Dependencies: git

# Usage: chkp

project_dir=~/Projects

if [[ ! -d $project_dir ]]; then
  echo "Directory '${project_dir}' not found! Exiting..."
  exit 1
elif [[ -z $(echo ${project_dir}/*(N)) ]]; then
  echo "Directory '${project_dir}' is empty! Exiting..."
  exit 1
fi

for directory in ${project_dir}/*; do
  cd $directory

  if [[ -n $(git rev-list origin..HEAD 2> /dev/null) ]]; then
    print -P "%B%F{cyan}${directory:t}:%f%b"
  elif [[ -n $(git status --short 2> /dev/null) ]]; then
    print -P "%B%F{red}${directory:t}:%f%b"
  elif [[ -d .git ]]; then
    clean_repos+=(${directory:t})
    continue
  else
    not_repos+=(${directory:t})
    continue
  fi

  git status --branch --short
  echo
done

print -P "%B%F{green}Clean repo(s):%f%b"
if [[ ${#clean_repos} -gt 0 ]]; then
  for repo_name in $clean_repos; do
    echo " * $repo_name"
  done
else
  echo " - None found"
fi

echo

print -P "%B%F{blue}Not Git repo(s):%f%b"
if [[ ${#not_repos} -gt 0 ]]; then
  for repo_name in $not_repos; do
    echo " * $repo_name"
  done
else
  echo " - None found"
fi
