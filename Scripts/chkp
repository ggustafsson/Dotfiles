#!/usr/bin/env zsh

# Description: This script checks the git status of all my projects.
#
# Dependencies: git
#
# Usage: chkp
#        chkp <path>
#
# Written by GÃ¶ran Gustafsson (gustafsson.g at gmail.com).
# License: BSD 3-Clause.

if [[ $# -eq 0 ]]; then
  project_dir=~/Projects
elif [[ $# -eq 1 ]]; then
  project_dir=$1
else
  echo "Usage: ${0:t} [PATH]..."
  exit
fi

if [[ ! -d $project_dir ]]; then
  echo "Directory '${project_dir}' not found! Exiting..."
  exit 1
elif [[ -z $(echo ${project_dir}/*(N)) ]]; then
  echo "Directory '${project_dir}' is empty! Exiting..."
  exit 1
fi

for directory in ${project_dir}/*; do
  if [[ ! -d $directory ]]; then
    continue
  fi

  cd $directory

  if [[ -d .git ]]; then
    if [[ -n $(git rev-list origin..HEAD 2> /dev/null) ]]; then
      print -P "%B%F{cyan}${directory:t}:%f%b"
      git status --branch --short
      echo
    elif [[ -n $(git status --short 2> /dev/null) ]]; then
      print -P "%B%F{red}${directory:t}:%f%b"
      git status --branch --short
      echo
    else
      clean_repos+=(${directory:t})
    fi
  else
    not_repos+=(${directory:t})
  fi

  cd -
done

print -P "%B%F{green}Clean repo(s):%f%b"
if [[ ${#clean_repos} -gt 0 ]]; then
  for repo_name in $clean_repos; do
    echo " * $repo_name"
  done
else
  echo " - None found"
fi

echo

print -P "%B%F{blue}Not Git repo(s):%f%b"
if [[ ${#not_repos} -gt 0 ]]; then
  for repo_name in $not_repos; do
    echo " * $repo_name"
  done
else
  echo " - None found"
fi
