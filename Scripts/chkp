#!/usr/bin/env zsh

# Description: Checks status of all Git repos under default projects path, or
# path specified through argument. State of repo is indicated through color.
# Checks if a TODO file exist at root level of repository.
#
# Example:
#   Ansible: master
#   Dot-Files: master + todo
#   Project-Assets: master
#   ...
#
# Usage: chkp
#        chkp <path>
#
# Creator: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ $# -eq 0 ]]; then
  projects_path=~/Projects
elif [[ $# -eq 1 ]]; then
  projects_path=$1
else
  echo "Usage: ${0:t} [PATH]..."
  exit
fi

if [[ ! -d $projects_path ]]; then
  echo "Directory '${projects_path}' not found! Exiting..."
  exit 1
fi

for directory in ${projects_path}/*(N); do
  if ! git -C $directory rev-parse --is-inside-work-tree &> /dev/null; then
    continue
  fi

  pushd $directory
  echo -n "${directory:t}: "
  branch=$(git branch --show-current 2> /dev/null)
  if [[ -n $(git status --porcelain 2> /dev/null) ]]; then
    print -Pn "%F{red}${branch}%f"
  else
    print -Pn "%F{green}${branch}%f"
  fi
  files=(.todo(N) .todo.*(N) TODO(N) TODO.*(N))
  if [[ ! -z $files ]]; then
    echo " + todo"
  else
    echo
  fi
  popd
done
