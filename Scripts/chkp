#!/usr/bin/env zsh

# Description: This script checks the git status of all my projects.
#
# Usage: chkp
#        chkp <path>
#
# Creator: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ $# -eq 0 ]]; then
  projects_path=~/Projects
elif [[ $# -eq 1 ]]; then
  projects_path=$1
else
  echo "Usage: ${0:t} [PATH]..."
  exit
fi

if [[ ! -d $projects_path ]]; then
  echo "Directory '${projects_path}' not found! Exiting..."
  exit 1
fi

echo "Checking Git repositories in '${projects_path}'."

for directory in ${projects_path}/*(N); do
  if [[ -d ${directory}/.git ]]; then
    echo -n "\n${directory:t}"
    if [[ -f ${directory}/.todo ]]; then
      print -P "  %F{blue}.todo%f"
    else
      echo
    fi
    git -C $directory status --branch --short
  fi
done
