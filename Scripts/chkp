#!/usr/bin/env zsh

# Description: Checks status of all git repos under default projects path, or
# path specified through argument. If file ".todo(.txt)" is found in directory
# it is displayed next to directory name.
#
# Usage: chkp
#        chkp <path>
#
# Creator: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ $# -eq 0 ]]; then
  projects_path=~/Projects
elif [[ $# -eq 1 ]]; then
  projects_path=$1
else
  echo "Usage: ${0:t} [PATH]..."
  exit
fi

if [[ ! -d $projects_path ]]; then
  echo "Directory '${projects_path}' not found! Exiting..."
  exit 1
fi

echo "Checking Git repos in '${projects_path:a}'."

for directory in ${projects_path}/*(N); do
  if [[ -d ${directory}/.git ]]; then
    pushd $directory
    echo -n "\n${directory:t}"
    if [[ -f .todo || -f .todo.txt ]]; then
      print -P " %F{blue}.todo%f"
    else
      echo
    fi
    git status --branch --short
    popd
  fi
done
