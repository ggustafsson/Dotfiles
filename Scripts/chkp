#!/usr/bin/env zsh

# Description: This script checks the git status of all my projects.
#
# Dependencies: git
#
# Usage: chkp
#        chkp <path>
#
# Written by GÃ¶ran Gustafsson (gustafsson.g at gmail.com).
# License: BSD 3-Clause.

if [[ $# -eq 0 ]]; then
  project_dir=~/Projects
elif [[ $# -eq 1 ]]; then
  project_dir=$1
else
  echo "Usage: ${0:t} [PATH]..."
  exit
fi

if [[ ! -d $project_dir ]]; then
  echo "Directory '${project_dir}' not found! Exiting..."
  exit 1
fi

for directory in ${project_dir}/*(N); do
  if [[ ! -d $directory ]]; then
    continue
  fi

  if [[ -d ${directory}/.git ]]; then
    pushd $directory
    if [[ -n $(git rev-list origin..HEAD 2> /dev/null) ]]; then
      print -P "${directory:t}: %B%F{yellow}$(git branch --show-current)%f%b"
    elif [[ -n $(git status --short 2> /dev/null) ]]; then
      print -P "${directory:t}: %B%F{red}$(git branch --show-current)*%f%b"
    else
      print -P "${directory:t}: %B%F{green}$(git branch --show-current)%f%b"
    fi
    popd
  else
    print -P "${directory:t}: not git repo"
  fi
done
