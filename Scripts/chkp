#!/usr/bin/env zsh

# Description: Checks status of all git repos under default projects path, or
# path specified through argument. If TODO file is found in directory it is
# displayed next to directory name.
#
# Usage: chkp
#        chkp <path>
#
# Creator: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ $# -eq 0 ]]; then
  projects_path=~/Projects
elif [[ $# -eq 1 ]]; then
  projects_path=$1
else
  echo "Usage: ${0:t} [PATH]..."
  exit
fi

if [[ ! -d $projects_path ]]; then
  echo "Directory '${projects_path}' not found! Exiting..."
  exit 1
fi

echo "Checking Git repos in '${projects_path:a}'."

for directory in ${projects_path}/*(N); do
  if [[ -d ${directory}/.git ]]; then
    pushd $directory
    echo -n "\n${directory:t}"
    files=(.todo(N) .todo.*(N) TODO(N) TODO.*(N))
    if [[ ! -z $files ]]; then
      print -P " - %F{magenta}todo%f"
    else
      echo
    fi
    git status --branch --short
    popd
  fi
done
