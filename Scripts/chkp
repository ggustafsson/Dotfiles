#!/usr/bin/env zsh

# Description: This script checks the git status of all my projects.
#
# Dependencies: git
#
# Usage: chkp
#        chkp <path>
#
# Written by GÃ¶ran Gustafsson (gustafsson.g at gmail.com).
# License: BSD 3-Clause.

if [[ $# -eq 0 ]]; then
  dir=~/Projects
elif [[ $# -eq 1 ]]; then
  dir=$1
else
  echo "Usage: ${0:t} [PATH]..."
  exit
fi

if [[ ! -d $dir ]]; then
  echo "Directory '${dir}' not found! Exiting..."
  exit 1
fi

echo "Checking Git repositories."

for subdir in ${dir}/*(N); do
  if [[ ! -d $subdir ]]; then
    continue
  fi

  if [[ -d ${subdir}/.git ]]; then
    pushd $subdir
    if [[ -n $(git status --porcelain 2> /dev/null) ]]; then
      echo "\n${subdir:t}"
      git status --branch --short
    else
      clean_repos+=(${subdir:t})
    fi
    popd
  fi
done

if [[ ${#clean_repos} -gt 0 ]]; then
  print -P "\n%B%F{green}Clean Git repositories:%f%b"
  for repo in $clean_repos; do
    echo " - $repo"
  done
fi
