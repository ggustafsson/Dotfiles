#!/usr/bin/env zsh

# Description: Pretty prints "LS_COLORS" setting.
#
# Usage: lscolors
#
# Original script found here:
#   <http://unix.stackexchange.com/questions/20721/testing-ls-colors-in-zsh>

if [[ ! $LS_COLORS ]]; then
  echo "Variable 'LS_COLORS' is not set! Exiting..."
  exit 1
fi

# Options here are based on output from "dircolors" version 9.0.
typeset -A names
names[bd]="block device"
names[ca]="capability"
names[cd]="character device"
names[di]="directory"
names[do]="door"
names[ex]="executable"
names[fi]="normal file"
names[ln]="symbolic link"
names[mh]="multiple hard links"
names[mi]="missing file"
names[no]="global default"
names[or]="orphan symlink"
names[ow]="other writable"
names[pi]="named pipe"
names[rs]="reset"
names[sg]="set gid"
names[so]="socket"
names[st]="sticky"
names[su]="set uid"
names[tw]="sticky other writable"

for item in ${(s.:.)LS_COLORS}; do
  key=${item%\=*}
  color=${item#*\=}
  name=${names[(e)$key]-$key}
  printf '\e[%sm%s\e[m\n' $color $name
done
