#!/usr/bin/env zsh

# Description: Backs up various files/directories to external device.
#
# Usage: backup
#
# Author: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ $1 != (-y|--yes) ]]; then
  confirm || exit 1
fi

destination=/Volumes/Keychain

# TODO: Adjust list after buying larger USB flash drive.
backup_objects=(
  ~/.ssh
  ~/Documents
  ~/Dropbox
  ~/Golang/src
  ~/Library/Safari/Bookmarks.plist
  ~/Movies
  ~/Music/MP3
  ~/Pictures
  ~/Projects
  ~/Sites
  ~/X-Files
)

if [[ ! -d $destination ]]; then
  echo "Directory '${destination}' does not exist! Exiting..."
  exit 1
fi

echo "Backing up to directory '${destination}'."

for object in $backup_objects; do
  if [[ -a $object ]]; then
    print -P "\n%F{green}Backing up: ${object/${HOME}/~}%f"
    rsync --archive --delete --human-readable --verbose \
      --exclude=Dropbox/.dropbox.cache \
      --exclude=Dropbox/Public \
      --exclude=Dropbox/Shared \
      $object $destination
  else
    echo "\nObject '${object}' not found! Exiting..."
    exit 1
  fi
done

echo "\nUnmounting external device."
while true; do
  sleep 5
  diskutil eject $destination && exit
done
