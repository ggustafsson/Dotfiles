#!/usr/bin/env zsh

# Description: Backs up miscellaneous files/directories to external device.
#
# Usage: backup
#        backup --yes
#
# Author: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ $1 != (-y|--yes) ]]; then
  confirm || exit 1
fi

destination=/Volumes/Keychain\ 1

backup_objects=(
  ~/.ssh
  ~/Documents
  ~/Dropbox
  ~/Golang
  ~/Movies
  ~/Music
  ~/Pictures
  ~/Playground
  ~/Projects
  ~/Sites
  ~/Storage
)

if [[ ! -d $destination ]]; then
  echo "Directory '${destination}' does not exist! Exiting..."
  exit 1
fi

echo "Backing up to directory '${destination}'."

for object in $backup_objects; do
  if [[ -a $object ]]; then
    print -P "\n%B%F{green}Backing up: ${object/${HOME}/~}%f%b"
    rsync --archive --delete --human-readable --verbose \
      --exclude=Dropbox/.dropbox.cache \
      --exclude=Dropbox/Public \
      --exclude=Dropbox/Shared \
      $object $destination
  else
    echo "\nObject '${object}' not found! Exiting..."
    exit 1
  fi
done

echo "\nUnmounting external device."
while true; do
  sleep 5
  diskutil eject $destination && exit
done
