#!/usr/bin/env zsh

# Description: This script backs up a few specific directories/files to device.
#
# Usage: backup
#
# Creator: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ ! $1 == (-y|--yes) ]]; then
  confirm || exit 1
fi

destination=/Volumes/Keychain

backup_objects=(
  ~/.ssh
  ~/Development
  ~/Documents
  ~/Golang/src
  ~/Library/Application\ Support/OpenEmu
  ~/Library/Mobile\ Documents/com\~apple\~CloudDocs
  ~/Library/Safari/Bookmarks.plist
  ~/Movies
  ~/Music/MP3
  ~/Pictures
  ~/Projects
  #~/School
  ~/Sites
  ~/Storage
  ~/Web\ Archives
)

if [[ ! -d $destination ]]; then
  echo "Directory '${destination}' does not exist! Exiting..."
  exit 1
fi

echo "Backing up to directory '${destination}'."

for object in $backup_objects; do
  if [[ -a $object ]]; then
    print -P "\n%B%F{green}Backing up: ${object/${HOME}/~}%f%b"
    rsync --archive --delete --human-readable --verbose $object $destination
  else
    echo "\nObject '${object}' not found! Exiting..."
    exit 1
  fi
done

echo "\nUnmounting external device."
while true; do
  sleep 5
  diskutil eject $destination && exit
done
