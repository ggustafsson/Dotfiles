#!/usr/bin/env zsh

# Description: Quickly run "cp file{,.bck}" on one or more files and optionally
# open in editor. Flag --nnn starts non-interactive mode for use as nnn plugin.
#
# ~/.zshrc:
#
#   export NNN_PLUG='b:!bck --nnn*'
#   alias vimbck="bck --edit"
#
# Usage: bck <file> ...
#        bck --edit <file> ...
#        bck --nnn
#
# Author: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ $# -lt 1 ]]; then
  echo "Usage: ${0:t} [OPTION]... [PATH(S)]...

  -e, --edit  Open file(s) with $VISUAL afterwards.
  -n, --nnn   Run as non-interactive nnn plugin."
  exit
fi

mkbck() {
  if [[ $1 == --nnn ]]; then
    cmd="cp -n"
    shift
  else
    cmd="cp -vi"
  fi

  for arg; do
    if [[ -f $arg ]]; then
      eval "$cmd '${arg}' '${arg}.bck'"
    fi
  done
}

if [[ $1 == (-n|--nnn) ]]; then
  selection=${NNN_SEL:-${XDG_CONFIG_HOME:-$HOME/.config}/nnn/.selection}
  if [[ -s $selection ]]; then
    IFS=$'\n'
    list=($(tr '\0' '\n' < $selection))
    mkbck --nnn $list
  elif [[ -s $nnn ]]; then
    mkbck --nnn $nnn
  fi
else
  if [[ $1 == (-e|--edit) ]]; then
    shift
    mkbck $*
    echo -n "\nPress 'Return' to continue..."
    read -s wait_for_input
    echo
    eval ${VISUAL:=nvim} $*
  else
    mkbck $*
  fi
fi
