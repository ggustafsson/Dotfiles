#!/usr/bin/env zsh

# Description: Converts FLAC files to high quality MP3 files. Automatic title
# capitalization of tags are enabled by default and depends on "title" script,
# disable feature with -i flag.
#
# Dependencies: flac and lame. title is optional.
#
# Usage: flaccon <file>
#        flaccon --ignore-case <file>
#        flaccon *.flac
#        find . -type f -iname "*.flac" -exec flaccon "{}" \;
#
# Author: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ $# -eq 0 ]]; then
  echo "Usage: ${0:t} [OPTION]... [FILENAME(S)]...

  -i, --ignore-case  Don't fix capitalization in ID3 tags."
  exit
fi

echo "Converting FLAC file(s) to MP3 file(s)."

if [[ $1 == (-i|--ignore-case) ]]; then
  capitalize=0
  shift # Removes $1 from argument list.
else
  if whence -p title &> /dev/null; then
    capitalize=1
  else
    echo "\nCommand 'title' not found! Skipping capitalization..."
    capitalize=0
  fi
fi

for arg; do
  if [[ ! -f $arg ]]; then
    echo "\nFile '${arg}' does not exist! Skipping..."
    continue
  fi

  if [[ $arg != *.flac ]]; then
    echo "\nFile '${arg}' does not end with '.flac'! Skipping..."
    continue
  fi

  # All tags that will be kept.
  tags=(title artist album tracknumber totaltracks genre date)

  # Put all tag information into shell variables.
  for tag in $tags; do
    output=$(metaflac --show-tag=$tag $arg) # "TITLE=Text".
    value=${output#*=} # Remove tag name, keep text.

    # Title capitalize non-empty values if feature is not disabled.
    if [[ -n $value && $capitalize -eq 1 ]]; then
      value=$(title $value)
    fi

    # Save value in tag variable. e.g. "title=Text".
    typeset ${tag}=$value
  done

  print -P "\n%B%F{cyan}Converting file: ${arg/${HOME}/~}%f%b\n"

  # Print out all tag information.
  for tag in $tags; do
    echo "${(C)tag}: ${(P)tag}"
  done

  # Decode FLAC and encode MP3. 320kbit/s bitrate and quality level 2.
  flac -dc $arg | lame -b 320 -q 2 --ignore-tag-errors \
    --tt "$title" \
    --ta "$artist" \
    --tl "$album" \
    --tn "${tracknumber}/$totaltracks" \
    --tg "$genre" \
    --ty "$date" \
    --add-id3v2 - ${arg:r}.mp3 # Remove file ending and add ".mp3".
done
