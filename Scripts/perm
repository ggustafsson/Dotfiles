#!/usr/bin/env zsh

# Description: Change permissions on files and directories recursively.
# Permissions used are 755/700 for directories and 644/600 for files (755/700
# are used if any prior executable flag is found). Under macOS ACL attributes
# are removed.
#
# Usage: perm --loose <path1> <path2> ...
#        perm --strict <path1> <path2> ...
#
# Author: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ $# -ge 2 && $1 == (-l|--loose) ]]; then
  mode_dir=755
  mode_file=644
  mode_exec=$mode_dir
elif [[ $# -ge 2 && $1 == (-s|--strict) ]]; then
  mode_dir=700
  mode_file=600
  mode_exec=$mode_dir
else
  echo "Usage: ${0:t} [OPTION]... [PATH(S)]...

  -l, --loose   Use permissions 755 on dirs and 644 on files.
  -s, --strict  Use permissions 700 on dirs and 600 on files."
  exit
fi

shift # Remove flag for argument list.

for arg; do
  if [[ ! -a $arg ]]; then
    echo "Item '${arg}' does not exist! Skipping..."
    continue
  fi

  echo "Changing permissions on '${arg}'."

  if [[ $OSTYPE == darwin* ]]; then
    find $arg -type d              -exec chmod -N "{}" \; -exec chmod $mode_dir "{}" \;
    find $arg -type f   -perm +111 -exec chmod -N "{}" \; -exec chmod $mode_exec "{}" \;
    find $arg -type f ! -perm +111 -exec chmod -N "{}" \; -exec chmod $mode_file "{}" \;
  else
    find $arg -type d              -exec chmod $mode_dir "{}" \;
    find $arg -type f   -perm /111 -exec chmod $mode_exec "{}" \;
    find $arg -type f ! -perm /111 -exec chmod $mode_file "{}" \;
  fi
done
